/*******************************************************************************
 * Copyright (c) 2010-2016, Tamas Borbas, IncQuery Labs Ltd.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *   Tamas Borbas - initial API and implementation
 *******************************************************************************/
package org.eclipse.viatra.query.runtime.cps.tests.queries

import "http://org.eclipse.viatra/model/cps"

pattern ReachableStates(
	s1 : State,
	s2 : State
) {
	State.outgoingTransitions(s1, t);
	Transition.targetState(t, s2);
}
pattern AllReachableStates(
	s1 : State,
	s2 : State
) {
	find ReachableStates+(s1, s2);
}

@Constraint(
	severity = "warning",
	key = {sm, state},
	message = "$state.identifier$ is unreachable from the initial state of $sm.identifier$"
)
pattern UnreachableStatesOfStateMachines(
	sm : StateMachine,
	state : State
) {
	StateMachine.initial(sm, init);
	StateMachine.states(sm, state);
	state!=init;
	neg find AllReachableStates(init, state);
}