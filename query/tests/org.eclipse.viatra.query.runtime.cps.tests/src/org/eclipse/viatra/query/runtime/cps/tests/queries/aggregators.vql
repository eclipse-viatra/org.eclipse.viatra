/*
 * Copyright (c) 2004-2017 Balazs Grill, Tamas Szabo, Gabor Bergmann and IncQuery Labs Ltd.
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-v20.html.
 * 
 * SPDX-License-Identifier: EPL-2.0
 */
package org.eclipse.viatra.query.runtime.cps.tests.queries

import "http://www.eclipse.org/emf/2002/Ecore"
import "http://org.eclipse.viatra/model/cps"
 
pattern sumPriority(s : java Integer) {
	s == sum find hostPriority(_h, _aT, #c);
}
pattern sumPriorityEmbedded(s : java Integer) {
	s == sum HostInstance.applications.priority(_, #);
}

pattern minPriority(m : java Integer) {
	m == min find hostPriority(_h, _aT, #c);
}
pattern minPriorityEmbedded(m : java Integer) {
	m == min HostInstance.applications.priority(_, #);
}

pattern maxPriority(m : java Integer) {
	m == max find hostPriority(_h, _aT, #c);
}

pattern maxPriorityEmbedded(m : java Integer) {
	m == max HostInstance.applications.priority(_, #);
}

pattern avgCPU(n : java Integer) {
	find sumCPU(s);
	find countHost(c);
	n == eval(if (c == 0) null else (s / c));
}

pattern avgCPUEmbedded(n : java Integer) {
	find sumCPUEmbedded(s);
	find countHostEmbedded(c);
	n == eval(if (c == 0) null else (s / c));
}


pattern avgCPU2(n : java Double) {
    n == avg find availableCPU(_, #c);
}

pattern avgCPU2Embedded(n : java Double) {
    n == avg HostInstance.availableCpu(_, #c);
}

pattern avgRamRequirement(n : java Double) {
	n == avg find ramRequirement(_, #r);
}
 
pattern avgRamRequirementEmbedded(n : java Double) {
	n == avg ResourceRequirement.requiredRam(_, #r);
} 

pattern sumCPU(n : java Integer) {
	n == sum find availableCPU(_, #c);
}

pattern sumCPUEmbedded(n : java Integer) {
	n == sum HostInstance.availableCpu(_, #c);
}

pattern countHost(n : java Integer) {
	n == count find hostInstance(_);
}

pattern countHostEmbedded(n : java Integer) {
	n == count HostInstance(_);
}

pattern minCPU(n : java Integer) {
	n == min find availableCPU(_, #c);
}

pattern minCPUEmbedded(n : java Integer) {
	n == min HostInstance.availableCpu(_, #c);
}

pattern hostInstanceWithMinCPU1(host : HostInstance, value : java Integer) {
	value == min find availableCPU(_, #c);
	find availableCPU(host, value);
}


pattern hostInstanceWithMinCPU2(host : HostInstance, value : java Integer) {
	find minCPU(value);	
	find availableCPU(host, value);
}
pattern hostInstanceWithMinCPUEmbedded(host : HostInstance, value : java Integer) {
	value == min HostInstance.availableCpu(_, #);
	HostInstance.availableCpu(host, value);
}

pattern hasOddApplications(hostInst: HostInstance, appInst : ApplicationInstance) = {
	CT == count find manyParams(hostInst, _a, _b, appInst, _c);
	check (CT % 2 == 1);
}


// HELPER PATTERNS
pattern availableCPU(host : HostInstance, value : java Integer) {
	HostInstance.availableCpu(host, value);
}

pattern hostPriority(host : HostInstance, appType : ApplicationType, priority : java Integer) {
	HostInstance.applications(host, app);
	ApplicationInstance.type(app, appType);
	ApplicationInstance.priority(app, priority);
}

pattern hostInstance(host : HostInstance) {
	HostInstance(host);
}

pattern manyParams(hostInst: HostInstance, a : java Integer, b : java Integer, appInst : ApplicationInstance, c : java Integer) = {
	a == 1;
	b == 2;
	c == 3;
	HostInstance.applications(hostInst, appInst);
}

private pattern ramRequirement(req : ResourceRequirement, ram : java Integer) {
	ResourceRequirement.requiredRam(req, ram);
}