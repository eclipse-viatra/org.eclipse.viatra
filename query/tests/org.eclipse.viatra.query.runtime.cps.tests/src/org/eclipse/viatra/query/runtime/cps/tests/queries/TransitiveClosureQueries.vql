package org.eclipse.viatra.query.runtime.cps.tests.queries

import "http://org.eclipse.viatra/model/cps"

private pattern stateTransition(source : State, target : State) {
	State.outgoingTransitions(source, tr);
	Transition.targetState(tr, target);
}

pattern statesTC(s1 : State, s2 : State) {
	find stateTransition+(s1, s2);
}

pattern statesTCEmbedded(s1 : State, s2 : State) {
	State.outgoingTransitions.targetState+(s1, s2);
}

pattern statesRTC1(s1 : State, s2 : State) {
	find stateTransition+(s1, s2);
} or {
	s1 == s2;
}

pattern statesRTC2(s1 : State, s2 : State) {
	find stateTransition*(s1, s2);
}

pattern statesRTCEmbedded(s1 : State, s2 : State) {
	State.outgoingTransitions.targetState*(s1, s2);
}